import Mathlib
open Real
open Set

/-
# Bolzano-Weierstrass or sequential compactness theorem

In this file we will formalize Bolzano-Weierstrass using the proof from wikipedia: <https://en.wikipedia.org/wiki/Bolzano%E2%80%93Weierstrass_theorem#Proof>

We begin with `monotone_of_frequently_increasing` after which a more general result `eventually_monotone_of_frequently_increasing` is proven. Then the key lemma `subseq_monotone_or_antitone` and finally the theorem `tendsto_subseq_of_bddAbove_bddBelow`.

Good luck!
-/

/--
## Definition: Peak
Let us call an index `n` of a sequence a "peak" of the sequence when all values in the sequence that come after `n` are less than or equal to the peak.
-/
def IsPeak (x : â„• â†’ â„) (n : â„•) := âˆ€ m > n, x m â‰¤ x n

-- Set of all peaks is infinite
def setOf_peaks_infinite (x : â„• â†’ â„) := { n | IsPeak x n }.Infinite

-- This is useful for working with the proof terms (â‹¯) that appear in the following lemma
set_option pp.proofs true in

/--
Lemma: Every frequently increasing sequence `(xâ‚™)` has a monotone subsequence. I.e. for each index `n` there is further index `m` such that `xâ‚™ â‰¤ xâ‚˜`.

We use `Ï†` to refer to a renumbering of indices such that `x âˆ˜ Ï†` is the monotone subsequence.

Coming up with `Ï†` requires a tricky technique involving recursively choosing the next index that comes after the previous one.

Proof:

Let `Ï†â‚€` be the next index at which `x Ï†â‚€ > xâ‚€` (given by `h`). Inductively define `Ï† (n + 1)` to be the next index at which `x (Ï† (n + 1)) > x Ï†â‚™`.

First, to show strict monotonicity of `Ï†`, i.e. `a < b âŠ¢ Ï† a < Ï† b` we use induction on `b`. In the base case we use the fact that `a` must equal `0` and `(h (Ï† 0)).choose_spec` to get `Ï† 0 < Ï† 1`. In the inductive case we use `(h (Ï† (b + 1))).choose_spec` to get the induction hypothesis `Ï† (b + 1 + 1) > Ï† (b + 1)` (formed when constructing `Ï†`) as well as the induction hypothesis `a < b + 1 â†’ Ï† a < Ï† (b + 1)`.

To show monotonicity of `x âˆ˜ Ï†`, i.e. `a â‰¤ b âŠ¢ x (Ï† a) â‰¤ x (Ï† b)` is similar, but we are working with â‰¤ instead of <.
-/
lemma monotone_of_frequently_increasing {x : â„• â†’ â„} (h : âˆ€ (n : â„•), âˆƒ m > n, x n â‰¤ x m) : âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ Monotone (x âˆ˜ Ï†) := by
  -- Magic ingredient: the indices of the subsequence are generated iteratively with choice from `h`
  let Ï† (n : â„•) : â„• := by
    induction n
    case zero
    Â· exact (h 0).choose
    case succ _ Ï†n
    Â· exact (h Ï†n).choose -- Call h with the result of `Ï† n`

  use Ï†
  constructor
  Â· intro a b hab
    -- Offset `b` by one as `Ï† 0` is not less than `Ï† 0`
    have : âˆƒ b', b = b' + 1 := âŸ¨b - 1, (Nat.sub_add_cancel (by linarith)).symmâŸ©
    obtain âŸ¨b, eqâŸ© := this
    subst eq

    induction b
    case zero
    Â· have : a = 0
      Â· linarith
      subst a
      exact (h (Ï† 0)).choose_spec.1
    case succ b bih
    Â· have hb := (h (Ï† (b + 1))).choose_spec.1
      -- Notice that `(h (Ï† (b + 1))).choose â‰¡ (Ï† (b + 1 + 1))`
      by_cases hsa : a = b + 1
      Â· subst a
        exact hb
        -- exact Nat.zero_lt_succ b
      Â· apply lt_trans' hb
        apply bih
        apply lt_of_le_of_ne
        Â· linarith
        Â· exact hsa

  Â· intro a b hab
    induction b
    case zero
    Â· have : a = 0
      Â· linarith
      subst a
      exact le_rfl
    case _ b bih
    Â· have hb := (h (Ï† (b))).choose_spec.2
      by_cases hsa : a = b + 1
      Â· subst a
        exact le_rfl
      Â· apply le_trans' hb
        apply bih
        apply Nat.le_of_lt_add_one
        apply lt_of_le_of_ne
        Â· linarith
        Â· exact hsa

/--
Lemma: Every eventually frequently increasing sequence `(xâ‚™)` has a monotone subsequence. I.e. for some `N` and each index `n > N` there is further index `m` such that `xâ‚™ â‰¤ xâ‚˜`.

The proof is very simple, offset every index by `N + 1` and apply `monotone_of_frequently_increasing`. Convincing Lean is the hard part.


-/
lemma eventually_monotone_of_frequently_increasing {x : â„• â†’ â„} (N : â„•) (h : âˆ€ a > N, âˆƒ b > a, x a â‰¤ x b) : âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ Monotone (x âˆ˜ Ï†) := by
  have h' : (âˆ€ (a : â„•), âˆƒ b > a, (x âˆ˜ fun x => x + N + 1) a â‰¤ (x âˆ˜ fun x => x + N + 1) b)
  Â· intro a
    obtain âŸ¨b, bhâŸ© := h (a + N + 1) (by linarith)

    have nb1 : N â‰¤ b - 1
    Â·
      rw [â† add_le_add_iff_right 1]
      rw [Nat.sub_add_cancel]
      linarith
      linarith

    use (b - 1 - N)
    constructor
    Â· -- this is annoyingly verbose
      rw [gt_iff_lt]
      rw [â† add_lt_add_iff_right N]
      rw [â† add_lt_add_iff_right 1]
      rw [Nat.sub_add_cancel]
      rw [Nat.sub_add_cancel]
      exact bh.1
      linarith
      exact nb1
    Â· simp
      rw [Nat.sub_add_cancel]
      rw [Nat.sub_add_cancel]
      exact bh.2
      linarith
      exact nb1

  -- Now we use the previous lemma to get a monotone subsequence
  have := monotone_of_frequently_increasing (x := x âˆ˜ (. + N + 1))
  specialize this h'
  obtain âŸ¨Ï†, Ï†_mono, Ï†hâŸ© := this

  -- Now we use the shifted renumbering
  use (. + N + 1) âˆ˜ Ï†
  constructor
  Â· intro a b hab
    simp only [Function.comp_apply, add_lt_add_iff_right]
    exact Ï†_mono hab
  Â· exact Ï†h

/--
Lemma: Every infinite sequence `(xâ‚™)` in â„ has an infinite monotone or antitone subsequence (a subsequence that is either non-decreasing or non-increasing).

This is the key lemma in proving Bolzano-Weierstrass, and the first section of the proof on wikipedia <https://en.wikipedia.org/wiki/Bolzano%E2%80%93Weierstrass_theorem#Proof>. Follow that proof and formalize every step.

Useful theorems:
- `Set.Infinite.exists_gt`
- `antitone_iff_forall_lt`
- `Set.not_infinite`
- `Set.Finite.exists_finset`
-/
lemma subseq_monotone_or_antitone (x : â„• â†’ â„) :
  (âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ Monotone (x âˆ˜ Ï†)) âˆ¨
  (âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ Antitone (x âˆ˜ Ï†)) := by

  -- Start by case splitting on `setOf_peaks_infinite x`
  by_cases h : setOf_peaks_infinite x <;> rw [setOf_peaks_infinite] at h
  Â· -- Suppose first that the sequence has infinitely many peaks,
    -- which means there is an antitone subsequence defined by those peaks
    right

    -- First, we show nonemptyness of the set of peaks
    have ne : Nonempty {n | IsPeak x n}
    Â· obtain âŸ¨x, xhâŸ© := h.nonempty
      use x

    -- Next, we show that the set of peaks has no largest element.
    -- Here we need to work with subtypes because the elements are of type `Elem {n | IsPeak x n}`. If you have a `n : â„•` and `nh : IsPeak x n`, you can form such an element using `âŸ¨n, nhâŸ©`
    have nmo : NoMaxOrder {n | IsPeak x n} := {
      exists_gt := by
        intro a
        obtain âŸ¨b, bhâŸ© := Set.Infinite.exists_gt h a
        use âŸ¨b, bh.1âŸ©
        exact bh.2
    }

    -- Select `Ï†` such that it gives all peaks in order.
    -- `Nat.exists_strictMono` is very convenient for this.
    #check Nat.exists_strictMono
    have âŸ¨Ï†, Ï†_monoâŸ© := Nat.exists_strictMono {n | IsPeak x n}

    -- There is probably a lemma about showing existence with subtype in the codomain implies existence which we could use here, but I just used `Subtype.val âˆ˜ Ï†` and `Subtype.val_prop` inside the cases to get the subtype predicate.
    use Subtype.val âˆ˜ Ï†
    constructor
    Â· exact Ï†_mono
    Â· rw [antitone_iff_forall_lt]
      intro a b h
      let Ï†a := Ï† a
      have Ï†ah := mem_setOf.mp (Subtype.val_prop Ï†a)

      -- We know that because Ï† a is a peak, then for all b > a, x (Ï† b) â‰¤ x (Ï† a)
      -- Note that Ï† b can be a peak but it doesn't have to be
      rw [IsPeak] at Ï†ah
      apply Ï†ah

      -- Therefore we need to show that Ï† b > Ï† a, which we know by monotonicity of Ï†
      exact Ï†_mono h

  Â·
    by_cases ne : Set.Nonempty {n | IsPeak x n}
    Â· -- x has finitely many peaks (but at least one peak),
      -- which means there is a monotone subsequence starting after the last peak
      left
      rw [Set.not_infinite] at h
      have âŸ¨s, shâŸ© := Set.Finite.exists_finset h

      obtain âŸ¨n, nhâŸ© := ne
      apply (sh n).mpr at nh
      have sne : s.Nonempty := âŸ¨n, nhâŸ©

      -- N is the index of the final peak
      let N := s.max' sne

      -- Now we should show that after `N` there are no more peaks
      have no_more_peaks : Â¬ âˆƒ m > N, IsPeak x m
      Â· intro âŸ¨m, mh1, mh2âŸ©
        simp only [N] at mh1

        -- All elements in the set are smaller than the maximum
        have := Finset.le_max' s m $ (sh _).mpr mh2
        -- have : m â‰¤ N := this -- these are the same

        have := lt_of_lt_of_le mh1 this
        simp only [lt_self_iff_false] at this

      -- Using `no_more_peaks` we can show that the sequence must increase frequently
      have frequently_increasing : âˆ€ a > N, âˆƒ b > a, x a â‰¤ x b
      Â·
        push_neg at no_more_peaks
        simp_rw [IsPeak] at no_more_peaks
        intro a ah
        specialize no_more_peaks _ ah
        push_neg at no_more_peaks
        obtain âŸ¨m, mhâŸ© := no_more_peaks
        use m
        constructor <;> linarith

      -- Here we get to use `eventually_monotone_of_frequently_increasing` now that we know `N` is the final peak
      exact eventually_monotone_of_frequently_increasing N frequently_increasing

    Â· -- There are no peaks, therefore there must be a monotone subsequence
      left

      -- Using the fact that the set of peaks is empty, like before, we can show that the sequence must increase frequently
      have frequently_increasing : âˆ€ a, âˆƒ b > a, x a â‰¤ x b
      Â·
        push_neg at ne
        simp_rw [eq_empty_iff_forall_not_mem, mem_setOf] at ne
        simp_rw [IsPeak] at ne
        intro a
        specialize ne a
        push_neg at ne
        obtain âŸ¨m, mhâŸ© := ne
        use m
        constructor <;> linarith

      -- And after applying `monotone_of_frequently_increasing` we are left with a completed proof.
      exact monotone_of_frequently_increasing frequently_increasing

open Filter
open Topology

/--
## Bolzano-Weierstrass theorem in â„.

The theorem states that each infinite bounded sequence in â„ has a convergent subsequence.

An equivalent formulation is that a subset of â„ is sequentially compact if and only if it is closed and bounded.

Useful theorems:
- `tendsto_of_monotone` and `tendsto_of_antitone`
- `Filter.unbounded_of_tendsto_atTop` and `Filter.unbounded_of_tendsto_atBot`
- `range_subset_range_iff_exists_comp`
-/
theorem tendsto_subseq_of_bddAbove_bddBelow {x : â„• â†’ â„} (hx : BddAbove (range x) âˆ§ BddBelow (range x)) : âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆƒ a, Tendsto (x âˆ˜ Ï†) atTop (ğ“ a) := by
  -- Because every sequence has a monotone or antitone subsequence, we can case split on it
  cases' subseq_monotone_or_antitone x with subseq subseq
  Â· -- Monotone case
    obtain âŸ¨Ï†, Ï†_mono, Ï†hâŸ© := subseq
    use Ï†
    refine âŸ¨Ï†_mono, ?_âŸ©

    -- Monotone convergence theorem: `Ï†` either tends to some value or diverges
    cases' tendsto_of_monotone Ï†h with subseq subseq
    Â· -- Derive a contradiction using the fact that `x` is bounded from above
      have := Filter.unbounded_of_tendsto_atTop subseq
      exfalso
      apply this

      have range_subset : range (x âˆ˜ Ï†) âŠ† range x
      Â· rw [range_subset_range_iff_exists_comp]
        use Ï†

      rw [not_bddAbove_iff] at this
      rw [bddAbove_def] at *
      obtain âŸ¨n, nhâŸ© := hx.1
      refine âŸ¨n, fun y yh â†¦ ?_âŸ©
      apply nh
      exact range_subset yh
    Â· exact subseq
  Â· obtain âŸ¨Ï†, Ï†_mono, Ï†hâŸ© := subseq
    use Ï†
    refine âŸ¨Ï†_mono, ?_âŸ©
    cases' tendsto_of_antitone Ï†h with subseq subseq
    Â· -- Derive a contradiction using the fact that `x` is bounded from below
      have := Filter.unbounded_of_tendsto_atBot subseq
      exfalso
      apply this

      have range_subset : range (x âˆ˜ Ï†) âŠ† range x
      Â· rw [range_subset_range_iff_exists_comp]
        use Ï†

      rw [not_bddBelow_iff] at this
      rw [bddBelow_def] at *
      obtain âŸ¨n, nhâŸ© := hx.2
      refine âŸ¨n, fun y yh â†¦ ?_âŸ©
      apply nh
      exact range_subset yh
    Â· exact subseq
